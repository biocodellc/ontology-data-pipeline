prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ppo: <http://www.plantphenology.org/id/>
prefix urn: <urn:>

# Use the concat/group_concat function to push all plantStructurePresence types into an array within a field.
# this enables ElasticSearch to index this easily while shrinking output file sizes
SELECT  
(?phenologyObservingProcess as ?eventId) ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?typePlant ?lower_count ?upper_count ?lower_percent ?upper_percent ?source ?subSource ?adjustedNcepReanalysisMonthlyMeanTemp (group_concat(distinct ?traitType;separator='|') as ?plantStructurePresenceTypes)

WHERE {    
        ?plant a ?typePlant . 

        ?plant dwc:genus ?genus . 
        OPTIONAL {?plant dwc:specificEpithet ?specificEpithet} . 
        OPTIONAL {?plant dwc:scientificName ?scientificName} . 

	# Join plantStructurePresence to plant
        ?plantStructurePresence obo:RO_0000080 ?plant .

	# Setup queries for the rdf:type of the plantStructurePresence (trait type) category
        ?plantStructurePresence rdf:type ?traitType .

	# remove declarations of owl:namedIndividual
	FILTER (?traitType != <http://www.w3.org/2002/07/owl#NamedIndividual>) .
	FILTER (?typePlant != <http://www.w3.org/2002/07/owl#NamedIndividual>) .
        ?plantStructurePresence obo:PPO_0000008 ?measurementDatum .

	# Join phenologyObservingProcess to measurementDatum
        ?measurementDatum obo:OBI_0000312 ?phenologyObservingProcess .

        # Set the type for phenologyObservingProcess and return properties
        ?phenologyObservingProcess rdf:type obo:PPO_0002000 .
        ?phenologyObservingProcess dwc:startDayOfYear ?dayOfYear .
        ?phenologyObservingProcess dwc:year ?year .
        ?phenologyObservingProcess dwc:decimalLatitude ?latitude .
        ?phenologyObservingProcess dwc:decimalLongitude ?longitude .
	OPTIONAL {?phenologyObservingProcess dc:creator ?source} .
	OPTIONAL {?phenologyObservingProcess urn:subSource ?subSource} .
	OPTIONAL {?phenologyObservingProcess dwc:measurementValue ?adjustedNcepReanalysisMonthlyMeanTemp} .

	# Return properties of measurementDatum
        OPTIONAL {?measurementDatum obo:PPO_0000001 ?lower_count} .
        OPTIONAL {?measurementDatum obo:PPO_0000002 ?upper_count} .
        OPTIONAL {?measurementDatum obo:PPO_0000003 ?lower_percent} .
        OPTIONAL {?measurementDatum obo:PPO_0000004 ?upper_percent} .
}
GROUP BY ?phenologyObservingProcess ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?typePlant ?lower_count ?upper_count ?lower_percent ?upper_percent ?source ?subSource ?adjustedNcepReanalysisMonthlyMeanTemp
ORDER BY ASC(?phenologyObservingProcess)
