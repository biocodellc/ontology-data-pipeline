prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ppo: <http://www.plantphenology.org/id/>
prefix urn: <urn:>

# Use the concat/group_concat function to push all plantStructurePresence types into an array within a field.
# this enables ElasticSearch to index this easily while shrinking output file sizes
SELECT  (?phenologicalObservingProcess as ?eventId) ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?basisOfRecord ?individualID ?eventRemarks ?lower_count_partplant ?upper_count_partplant ?lower_count_wholeplant ?upper_count_wholeplant ?lower_percent_partplant ?upper_percent_partplant ?lower_percent_wholeplant ?upper_percent_wholeplant ?source ?subSource (group_concat(distinct ?traitTypes;separator='|') as ?plantStructurePresenceTypes)

WHERE {    

	# Create a list of plantTraits for wholePlants these will be grouped by in the "GROUP BY" clause
	# NOTE that in attempting to look for trait types associated with part plants makes this sparql query super SLOW!!
	# if people need partPlantStructurePresences asserted (really only different for absence data, then we need
	# to alter this query at this point and look for partPlantStructurePresence Types)
        ?wholePlantStructurePresence rdf:type ?traitTypes .

	# Do not report namedindividual, not particularly useful in our output
        FILTER (?traitTypes != <http://www.w3.org/2002/07/owl#NamedIndividual>) .

	# The joins here are largely derived from the relations.csv file
	# since these can be different for different projects we need to think carefully
	# how to address joins in this section for the different projects.
	# Using SPARQL property paths is one possible solution
	?wholePlant rdf:type obo:PO_0000003 .
        ?partPlant obo:OBI_0000295 ?phenologicalObservingProcess .
	?phenologicalObservingProcess obo:OBI_0000299 ?partPlantMeasurementDatum .
	?phenologicalObservingProcess ?p ?partPlantMeasurementDatum .
	?wholePlantMeasurementDatum obo:PPO_0000014 ?partPlantMeasurementDatum .
	?wholePlantStructurePresence obo:PPO_0000008 ?wholePlantMeasurementDatum .
	?wholePlantStructurePresence obo:RO_0000080 ?wholePlant .
        ?partPlantStructurePresence obo:PPO_0000008 ?partPlantMeasurementDatum .
        ?partPlantStructurePresence obo:RO_0000080 ?partPlant .
	?partPlant rdf:type obo:PPO_0000015 .
	?partPlant obo:PPO_0000015 ?wholePlant .

	# Plant attributes
        ?wholePlant dwc:genus ?genus . 
        OPTIONAL {?wholePlant dwc:specificEpithet ?specificEpithet} . 
        OPTIONAL {?wholePlant dwc:scientificName ?scientificName} . 
        OPTIONAL {?wholePlant dwc:basisOfRecord ?basisOfRecord} . 
        OPTIONAL {?wholePlant dwc:individualID ?individualID} . 

        # PhenologicalObservingProcess Attributes
        ?phenologicalObservingProcess dwc:startDayOfYear ?dayOfYear .
        ?phenologicalObservingProcess dwc:year ?year .
        ?phenologicalObservingProcess dwc:decimalLatitude ?latitude .
        ?phenologicalObservingProcess dwc:decimalLongitude ?longitude .
	OPTIONAL {?phenologicalObservingProcess dc:creator ?source} .
	OPTIONAL {?phenologicalObservingProcess urn:subSource ?subSource} .
        OPTIONAL {?phenologicalObservingProcess dwc:eventRemarks ?eventRemarks} . 

	# Return properties of measurementDatum
        OPTIONAL {?partPlantMeasurementDatum obo:PPO_0000001 ?lower_count_partplant} .
        OPTIONAL {?partPlantMeasurementDatum obo:PPO_0000002 ?upper_count_partplant} .
        OPTIONAL {?wholePlantMeasurementDatum obo:PPO_0000001 ?lower_count_wholeplant} .
        OPTIONAL {?wholePlantMeasurementDatum obo:PPO_0000002 ?upper_count_wholeplant} .
        OPTIONAL {?partPlantMeasurementDatum obo:PPO_0000003 ?lower_percent_partplant} .
        OPTIONAL {?partPlantMeasurementDatum obo:PPO_0000004 ?upper_percent_partplant} .
        OPTIONAL {?wholePlantMeasurementDatum obo:PPO_0000003 ?lower_percent_wholeplant} .
        OPTIONAL {?wholePlantMeasurementDatum obo:PPO_0000004 ?upper_percent_wholeplant} .
}
GROUP BY ?phenologicalObservingProcess ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?basisOfRecord ?individualID ?eventRemarks ?lower_count_partplant ?upper_count_partplant ?lower_count_wholeplant ?upper_count_wholeplant ?lower_percent_partplant ?upper_percent_partplant ?lower_percent_wholeplant ?upper_percent_wholeplant ?source ?subSource 
ORDER BY ASC(?phenologyObservingProcess)
