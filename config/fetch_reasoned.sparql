prefix dwc: <http://rs.tdwg.org/dwc/terms/>
prefix obo: <http://purl.obolibrary.org/obo/>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#> 
prefix dc: <http://purl.org/dc/elements/1.1/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix ppo: <http://www.plantphenology.org/id/>

# Use the concat/group_concat function to push all plantStructurePresence types into an array within a field.
# this enables ElasticSearch to index this easily while shrinking output file sizes
SELECT  
?eventID ?recordID ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?lower_count ?upper_count ?lower_percent ?upper_percent ?source (group_concat(distinct ?traitType;separator='|') as ?plantStructurePresenceTypes)

WHERE {    
        ?wholePlant dwc:genus ?genus . 
        OPTIONAL {?wholePlant dwc:specificEpithet ?specificEpithet} . 
        OPTIONAL {?wholePlant dwc:scientificName ?scientificName} . 
        OPTIONAL {?wholePlant dwc:EventID ?recordID} .

	# Join plantStructurePresence to wholePlant
        ?plantStructurePresence obo:RO_0000080 ?wholePlant .

	# Setup queries for the rdf:type of the plantStructurePresence (trait type) category
        ?plantStructurePresence rdf:type ?traitType .

	# remove declarations of owl:namedIndividual
	FILTER (?traitType != <http://www.w3.org/2002/07/owl#NamedIndividual>) .
        ?plantStructurePresence obo:PPO_0001007 ?measurementDatum .

	# Join phenologyObservingProcess to measurementDatum
        ?measurementDatum obo:OBI_0000312 ?phenologyObservingProcess .

        # Set the type for phenologyObservingProcess and return properties
        ?phenologyObservingProcess rdf:type obo:PPO_0002000 .
        ?phenologyObservingProcess dwc:dayOfYear ?dayOfYear .
        ?phenologyObservingProcess dwc:year ?year .
        ?phenologyObservingProcess dwc:decimalLatitude ?latitude .
        ?phenologyObservingProcess dwc:decimalLongitude ?longitude .
	OPTIONAL {?phenologyObservingProcess dwc:EventID ?eventID} .
	OPTIONAL {?phenologyObservingProcess dc:creator ?source} .

	# Return properties of measurementDatum
        OPTIONAL {?measurementDatum obo:PPO_0001000 ?lower_count} .
        OPTIONAL {?measurementDatum obo:PPO_0001001 ?upper_count} .
        OPTIONAL {?measurementDatum obo:PPO_0001002 ?lower_percent} .
        OPTIONAL {?measurementDatum obo:PPO_0001003 ?upper_percent} .
}
GROUP BY ?eventID ?recordID ?dayOfYear ?year ?latitude ?longitude ?genus ?specificEpithet ?scientificName ?lower_count ?upper_count ?lower_percent ?upper_percent ?source
